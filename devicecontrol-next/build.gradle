/*
 * Copyright (C) 2013 - 2015 Alexander Martinz
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId "org.namelessrom.devicecontrol"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 131
        versionName '0.9.0'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName (versionName + '-git-' + gitRevision)
        }

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFile 'proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFile 'proguard.pro'
            minifyEnabled false
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    productFlavors {
        master { }
        next {
            applicationIdSuffix '.next'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    // until all third party packages got updated
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
    compile project(':modules:volley')

    // see https://github.com/ACRA/acra
    compile 'ch.acra:acra:4.7.0-RC.2'

    // see https://developer.android.com/tools/support-library/index.html
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'
    compile 'com.android.support:design:23.1.1'

    // see https://github.com/Evisceration/logger
    compile project(':modules:logger:library')

    // see https://github.com/Evisceration/ExecutionLibrary
    compile(project(':modules:ExecutionLibrary:library')) {
        exclude group: 'com.github.Evisceration', module: 'logger'
    }

    // see https://github.com/Evisceration/HardwareLibrary
    compile(project(':modules:HardwareLibrary:library')) {
        exclude group: 'com.github.Evisceration', module: 'logger'
    }

    // see https://github.com/Evisceration/MaterialPreferences
    compile(project(':modules:MaterialPreferences:library')) {
        exclude group: 'com.github.Evisceration', module: 'logger'
    }

    // see https://www.pollfish.com/android
    compile 'com.pollfish:pollfish:4.1.0:googleplayRelease@aar'
    compile 'com.google.android.gms:play-services-ads:8.3.0'

    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.0.1'
    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.0.16'

    // see https://github.com/f2prateek/rx-preferences
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.1'

    // see https://github.com/Kennyc1012/BottomSheet
    compile 'com.github.Kennyc1012:BottomSheet:2.0.4'

    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'

    // see https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.4.2@aar'
}
