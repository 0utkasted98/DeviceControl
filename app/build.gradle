apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'org.namelessrom.devicecontrol'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 153
        versionName '0.8.6.4'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName(versionName + '-git-' + gitRevision)
        }

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }

        def String keyPollFish = project.hasProperty("api_key_PollFish") ? api_key_PollFish : "---";
        buildConfigField('String', 'API_KEY_POLL_FISH', keyPollFish);

        def String keyGooglePlay = project.hasProperty("api_key_GooglePlay") ? api_key_GooglePlay : "---";
        buildConfigField('String', 'API_KEY_GOOGLE_PLAY', keyGooglePlay)

    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    productFlavors {
        google {}
        universal {}
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }
}

dependencies {
    compile project(':ExternalSources')

    if (useLocalSources) {
        logger.quiet("Using local sources...")
        // see https://github.com/Evisceration/ExecutionLibrary
        compile project(':ExecutionLibrary')
        // see https://github.com/Evisceration/HardwareLibrary
        compile project(':HardwareLibrary')
        // see https://github.com/Evisceration/MaterialPreferences
        compile project(':MaterialPreferences')
    } else {
        logger.quiet("Using external sources...")
        compile 'at.amartinz:execution:1.0.0'
        compile 'at.amartinz:hardware:1.0.1'
        compile 'com.github.Evisceration:MaterialPreferences:master-SNAPSHOT'
    }

    // see https://github.com/pilgr/Paper
    compile 'io.paperdb:paperdb:1.1'
    // see https://github.com/EsotericSoftware/kryo
    compile 'com.esotericsoftware:kryo:3.0.3'
    // see https://github.com/magro/kryo-serializers
    compile 'de.javakaffee:kryo-serializers:0.37'

    // see https://github.com/Evisceration/UniversalDebug
    compile 'at.amartinz:universal-debug:1.0.0'
    compile 'at.amartinz:universal-debug-fabric:1.0.0'
    // see https://github.com/JakeWharton/timber
    compile 'com.jakewharton.timber:timber:4.1.2'

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:support-v13:${supportLibVersion}"

    // see https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'

    // see https://www.pollfish.com/android
    googleCompile 'com.pollfish:pollfish:4.2.0:googleplayRelease@aar'
    universalCompile 'com.pollfish:pollfish:4.2.0:universalRelease@aar'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.android.gms:play-services-base:8.4.0'

    // see https://github.com/Kennyc1012/BottomSheet
    compile 'com.github.Kennyc1012:BottomSheet:2.1.2'

    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.0'
    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.1.5'
    // see https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    // see https://github.com/mcharmas/Android-ReactiveLocation
    compile 'pl.charmas.android:android-reactive-location:0.8@aar'

    // Sense360
    // see http://sense360.com/documentation.php?android
    compile 'com.sense360:sense360-quinoa:2.3.1@aar'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile('com.google.android.gms:play-services-gcm:8.4.0') {
        exclude module: 'play-services-measurement'
    }
    // see https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.6.2'
    // see https://github.com/square/okhttp
    compile 'com.squareup.okhttp:okhttp:2.7.5'

    // FABRIC
    // see https://fabric.io/kits/android/answers
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    // see https://fabric.io/kits/android/crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
