// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0-beta1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    // stfu gradle
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

def getGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    if (!new File(gitFolder + "HEAD").exists()) {
        return null
    }

    // how many characters we extract from the hash
    def takeFromHash = 6

    def head = new File(gitFolder + "HEAD").text.split(":")
    def isCommit = (head.length == 1)

    if (isCommit) {
        return head[0].trim().take(takeFromHash)
    }

    def refHead = new File(gitFolder + head[1].trim())
    return refHead.text.trim().take(takeFromHash)
}
