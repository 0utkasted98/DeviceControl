// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        useLocalSources = findProperty("we_are_awesome_developers") ?: false

        gradleVersion = '2.2.0-alpha4'

        targetSdk = 24
        buildTools = '24.0.0'

        supportLibVersion = '24.0.0'

        keyGooglePlay = findProperty("api_key_GooglePlay") ?: "\"---\"";
        keyPollFish = findProperty("api_key_PollFish") ?: "\"---\"";
    }

    repositories {
        jcenter()
        flatDir { dirs "aars" }
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${gradleVersion}"
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'

        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        // fabric
        classpath 'io.fabric.tools:gradle:1.21.5'
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir { dirs "aars" }
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://jitpack.io" }
    }
}

def getGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    if (!new File(gitFolder + "HEAD").exists()) {
        return null
    }

    // how many characters we extract from the hash
    def takeFromHash = 6

    def head = new File(gitFolder + "HEAD").text.split(":")
    def isCommit = (head.length == 1)

    if (isCommit) {
        return head[0].trim().take(takeFromHash)
    }

    def refHead = new File(gitFolder + head[1].trim())
    return refHead.text.trim().take(takeFromHash)
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
